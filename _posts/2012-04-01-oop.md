---
layout:  chapter
title:   CoffeeScript中的OOP
---

<div class="back"><a href="/cs/">&laquo; 返回目录</a></div>

##类：原型函数

CoffeeScript的类定义语法与它对象的定义语法很像。这并不是巧合，当你定义一个类时，其实你定义的就是一个对象。具体来讲，你定义的是一个原型。如果你定义了`constructor`函数，那它是唯一不属于原型的属性。  
让我们看一个例子，以阐明一个众所周知的事实：单个毛球族制造的麻烦与全部毛球族的数量成正比：

**Classes/Tribble.coffee**

{% highlight coffeescript %}
    class Tribble
      constructor: ->
        @isAlive = true
         Tribble.count++
     
       # Prototype properties
       breed: -> new Tribble if @isAlive
       die: ->
         Tribble.count-- if @isAlive
         @isAlive = false
      
       # Class-level properties
       @count: 0
       @makeTrouble: -> console.log ('Trouble!' for i in [1..@count]).join(' ')
{% endhighlight %}

这里有很多新语法，让我们分块讨论。  
每次新建一个毛球族时，`Trible.count`就加1（在这里我们可以将其称为`@count`，因为在类内部的`this`值就是类本身）。当调用`Trible.makeTrouble()`时，它会输出`Trible.count`次“Trouble”。  
测试一下：

**Classes/Tribble.coffee**

{% highlight coffeescript %}
    tribble1 = new Tribble
    tribble2 = new Tribble
    Tribble.makeTrouble() # "Trouble! Trouble!"
{% endhighlight %}

注意，在`Tribble`类的上下文中可以用`@count`来访问`Tribble.count`，但在`Tribble`的方法中却不可以。咋一看可能会有点莫名其妙，但是别忘记我们涉及到三个对象：`Tribble`对象本身（实际上就是`contructor`函数），`Tribble.prototype`，还有`Tribble`的实例。默认地，`Tribble`的属性（除去`contructor`之外的）都会附加到原型上。当使用`@`前缀时，就是明确表示我们想把该属性添加到类对象本身上去 。  
因为添加到原型上的函数（包括构造器）都是以各自的对象作为上下文被调用的，在这些函数中有@前缀的变量指向的都是实例属性。这就是我们在构造函数中定义`@isAlive`的原因：我们需要为每个实例添加各自的`@isAlive`属性。然后我们就可以这样做：

**Classes/Tribble.coffee**

{% highlight coffeescript %}
    tribble1.die()
    Tribble.makeTrouble() # "Trouble!"
{% endhighlight %}

由于有`if @isAlive`的检查，再次杀死`tribble1`就没什么影响了。而且众所周知，毛球族（tribbles）是胎生的，因此要不了多久就会有新的生物注入到我们的程序中：

**Classes/Tribble.coffee**

{% highlight coffeescript %}
    tribble2.breed().breed().breed()
    Tribble.makeTrouble() # "Trouble! Trouble! Trouble! Trouble!"
{% endhighlight %}

##使用“extends”来继承

到目前为止，我们讨论了原型是如何使得在大量对象之间共享各种功能变得容易的，以及CoffeeScript的类如何提供一个有用的语法来将原型属性绑到一起。如果这就是类所有能做的事情，那它似乎没多大用处。但是当我们想使用继承时类就会真正地闪闪发光起来。  
JavaScript是通过某个称为“原型链”的东西来实现继承的。假设，A的原型B有自己的原型C。然后我们写了这样的代码：

{% highlight coffeescript %}
    a = new A
    console.log a.flurb()
{% endhighlight %}

首先，运行时查看这个特殊的类A的实例a上是否有一个`flurb`的属性；如果没有则查看A的原型B；如果还是没找到，则它继续查看B的原型C。简而言之，它会遍历整个原型链。  
如果C上也没有flurb会怎么样？那运行时会检查原始对象的原型（即`{}`的原型）。也就是说，每个对象都继承了`{}`的原型，但是之间可能会包含其他原型。  
所有这些把原型给原型再给原型的赋值会变得有些混乱。这就是CoffeeScript中需要`extends`的原因。  
我们做个申明：

{% highlight coffeescript %}
    class B extends A
{% endhighlight %}

然后B的原型就继承自A的原型，另外还把A的类属性拷贝给了B。因此如果我们现在不再继续定义B，B的实例将有和A实例完全一样的行为。（有一个例外：`B.name`是“B”而`A.name`是“A”——name是一个特殊的属性。）  
让我们来看一个稍微深入点的例子：

{% highlight coffeescript %}
    class Pet
       constructor: -> @isHungry = true
       eat: -> @isHungry = false
    
    class Dog extends Pet
    eat: ->
      console.log '*crunch, crunch*'
       super()
    fetch: ->
      console.log 'Yip yip!'
       @isHungry = true
{% endhighlight %}

`Dog`继承了`Pet`的构造器，这意味着狗狗们生来就是饿的。当小狗吃东西时，它会发出一些声音然后调用`super()`，`super()`的意思是“调用父类同名的方法。”（精确地说就是`Pet::eat.call this`。）然后这只狗就不饿了。  
如果在子类上定义了一个构造函数，那它会覆盖父类的构造函数。不过它随时都可以用`super()`来调用父类的构造函数。在子类构造函数开始时就调用一下`super()`（或者更有可能用的`super`——参看*“super”不是“super”*，67页。）通常是明智之举。  
信不信由你，你已经知道了关于类的所有需要了解的知识。由于这些东西都是基于CoffeeScript的，所以语法可能与JavaScript有较大差别，但是编译后的代码是简单易懂的。如果你是一个传统OOP（面向对象编程）方法论的死忠，那下面这小节就是为你而设的了。

**多态与转型**  
类的一大应用就是多态。多态是一个高级的面向对象编程的术语——“某个对象可以被当作几种对象，但不是全部种类的对象”。下面是一个典型的例子：

{% highlight coffeescript %}
    class Shape
       constructor: (@width) ->
       computeArea: -> throw new Error('I am an abstract class!')
    
    class Square extends Shape
       computeArea: -> Math.pow @width, 2
    
    class Circle extends Shape
       radius: -> @width / 2
       computeArea: -> Math.PI * Math.pow @radius(), 2
    
    showArea = (shape) ->
       unless shape instanceof Shape
         throw new Error('showArea requires a Shape instance!')
       console.log shape.computeArea()
    
    showArea new Square(2) # 4
    showArea new Circle(2) # pi
{% endhighlight %}

注意到函数`showArea`会检查传入的对象是否是一个`Shape`的实例（使用`instanceof`关键字），但是它并不关心给它的是何种形状（Shape）。`Square`或者`Circle`实例都行。尽管这只是一个小示例，但是很难想象一个丰富的几何库不采用这种方式。  

<div class="addition">
<p><strong>“super”不是“super()”</strong></p>

<p>下面的代码有什么问题？</p>
<div class='highlight'><pre><code class='coffeescript'>    <span class='k'>class</span> <span class='nx'>Appliance</span>
       <span class='nv'>constructor: </span><span class='nf'>(warranty) -&gt;</span>
         <span class='nx'>warrantyDb</span><span class='p'>.</span><span class='nx'>save</span><span class='p'>(</span><span class='k'>this</span><span class='p'>)</span> <span class='k'>if</span> <span class='nx'>warranty</span>
    
    <span class='k'>class</span> <span class='nx'>Toaster</span> <span class='k'>extends</span> <span class='nx'>Appliance</span>
       <span class='nv'>constructor: </span><span class='nf'>(warranty) -&gt;</span>
         <span class='k'>super</span><span class='p'>()</span>
</code></pre>
</div>
<p>当我们创建一个新的<code>Toaster</code>时，<code>super()</code>没有照样传递<code>warranty</code>参数而是直接调用父类的构造函数，这意味着新的烤面包机（toaster）并不会被存储到担保（warranty）数据库中。<br />我们可以使用<code>super(warranty)</code>来解决这个问题，同时也可以用另一种简写方式：<code>super</code>。没有括号也没有参数的<code>super</code>会传递当前函数的所有参数。如果你是个Ruby程序员，这一定很眼熟。如果不是，那你就把<code>super</code>想象为一个非常非常贪婪的关键字吧——如果你没有告诉它你想传递哪些参数，它会传递所有的参数。</p>
</div>


如果我们不使用`instanceof`检查，这就会变成著名的“鸭子类型”（意思是，“如果它看起来像一只鸭子……” ）。就算相关对象没有`computeArea`方法，我们也总还是能得到一条有意义的错误信息。虽然鸭子类型很好，但是总有那么些时候你就是想要确定某个特定对象是否是如你所想的一样。  
在更加经典的面向对象的语言中，有一种结合switch来使用多态的惯用语法。我们还没有讨论过CoffeeScript中的`switch`，它与JavaScript中的`switch`有数处差别：首先，它在每个分句之间都有隐式的打断（`break`） 以防止意外的“落空”（fallthrough） ；其次，`switch`的运行结果会被用作它的返回值。（当该返回值被使用时，就不能使用`break`或`return`语句。如果你非要这样试试看，那你会得到一个像`Syntax-Error: cannot include a pure statement in an expression`这样的错误。用行话来说就是`a=return x`没有意义，因此编译器不会允许存在这种可能性。）  
CoffeeScript还做了一些语法上的改变，这在某种程度上提醒了JavaScript程序员注意那些隐藏着的差异：使用`when`代替了`case`，`else`代替了`default`。单个`when`后面可以跟几个潜在的匹配（matches），匹配之间用逗号隔开。同样，作为`:`的替代，也可以使用缩进 （或者then）把匹配分句与它们产生的结果隔开。  
下面是如何把它们合并到一个工厂函数中去的示例：

{% highlight coffeescript %}
    requisitionStarship = (captain) ->
       switch captain
          when 'Kirk', 'Picard', 'Archer'
           new Enterprise()
         when 'Janeway'
           new Voyager()
         else
           throw new Error('Invalid starship captain')
{% endhighlight %}

关于模块和类我们就讨论这些。你只要记住：CoffeeScript绝不会要求你必须使用类或者使用经典的面向对象的设计模式——毕竟，不使用它们，大多数JavaScript工程师也能干得很出色——但是对于某些程序来说，类就显得尤为适合。  
说到这里，还记得上一章程序中混乱的代码吗？来看看我们能对它们做点什么。
